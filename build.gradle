plugins {
    id 'java'
}

group = 'autotest'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    allure
}

dependencies {
    testImplementation group: 'com.consol.citrus', name: 'citrus-base', version: '3.4.0'
    testImplementation group: 'com.consol.citrus', name: 'citrus-testng', version: '3.4.0'
    testImplementation group: 'com.consol.citrus', name: 'citrus-spring', version: '3.4.0'
    testImplementation group: 'com.consol.citrus', name: 'citrus-http', version: '3.4.0'
    testImplementation group: 'com.consol.citrus', name: 'citrus-validation-json', version: '3.4.0'

    testImplementation group: 'org.slf4j', name: 'slf4j-reload4j', version: '2.0.7'

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    testImplementation 'io.qameta.allure:allure-testng:2.23.0'
    allure 'io.qameta.allure:allure-commandline:2.23.0'
}

tasks.register('generateAllureReport', JavaExec) {
    dependsOn test
    description = 'Генерирует HTML-отчёт Allure из результатов тестов'
    group = 'verification'

    mainClass = 'io.qameta.allure.CommandLine'
    classpath = configurations.allure

    args = [
            'generate',
            'build/allure-results',
            '--output', 'build/allure-report',
            '--clean'
    ]

    doFirst {
        file('build/allure-report').mkdirs()
    }

    ignoreExitValue = true
}

test {
    useTestNG()
    systemProperty 'allure.results.directory', file('build/allure-results')
}





